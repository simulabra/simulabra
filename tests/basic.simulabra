~class.new{
  :name=:point
  :components=(
    ~deffed
    ~var.new{
      :name=:x
      :type=:number
      :default=0
    }
    ~var.new{
      :name=:y
      :type=:number
      :default=0
    }
    ~method.new{
      :name=:dist
      :do=[_ ^.x.-(_.x).pow(2).+(.y.-(_.y).pow(2)).sqrt]
    }
    ~method.new{
      :name=:translate
      :do=[|%x %y|
        .x(.x.+(%x))
        .y(.y.+(%y))
      ]
    }
  )
}

~case.new{
  :name=:basic-points
  :do=[
    %p=~point.new{:x=3}
    %q=~point.new{:y=4}
    .assert-eq(%p.dist(%q) 5)
    %p.translate(1 3)
    .assert-eq(%p.dist(~point.new) 5)
  ]
}

~class.new{
  :name=:math
  :components=(
    ~static.new{
      :name=:pi
      :do=[^3.14159]
    }
  )
}

~class.new{
  :name=:circle
  :components=(
    ~point
    ~var.new{
      :name=:r
      :type=:number
      :default=1
    }
    ~method.new{
      :name=:area
      :do=[^~math.pi.*(.r.pow(2))]
    }
  )
}

~case.new{
  :name=:basic-circles
  :do=[
    %p=~circle.new{:x=3}
    %q=~circle.new{:y=4 :r=2}
    .assert-eq(%p.dist(%q) 5)
    .assert-eq(%p.area ~math.pi)
    .assert-eq(%q.area 4.*(~math.pi))
  ]
}

~case.new{
  :name=:basic-references
  :do=[
    %p=~point.new{:name=:test :x=7}
    .assert-eq(~point#test.x 7)
  ]
}

~case.new{
  :name=:basic-closures
  :do=[
    %fn=[_ ^_.*(2)]
    %q=%fn.do(3)
    .assert-eq(%q 6)
    %fnp=[^.x.*(2)]
    %p=~point.new{:x=2}
    .assert-eq(%fnp.apply(%p) 4)
  ]
}
