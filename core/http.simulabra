!import=(:base :lang :test)
!js-import=(:http)

~class{
  :name=:js-wrap
  :components=(
    ~var{
      :name=:inner
      :foreign=@true
    }
  )
}

~class{
  :name=:url
  :components=(
    ~js-wrap
    ~static{
      :name=:from-string
      :do=[|%url-string|
        ^.new{
          :inner=~js-node{:js=`"new URL(\"${%url-string}\")"}
        }
      ]
    }
  )
}

~class{
  :name=:request-handler
  :components=(
    ~var{:name=:server}
    ~var{:name=:path}
    ~var{:name=:do}
    ~after{
      :name=:init
      :do=[.server.add-handler(.me)]
    }
    ~method{
      :name=:handle
      :do=[|%req %res| .do(%req %res)]
    }
  )
}

~class{
  :name=:http-server
  :components=(
    ~js-wrap
    ~var{
      :name=:handlers
      :default=()
    }
    ~method{
      :name=:add-handler
      :do=[|%handler| .handlers.push(%handler)]
    }
    ~method{
      :name=:path-handler
      :do=[|%path|
        ^.handlers.find([_ ^_.path.eq(%path)])
      ]
    }
    ~method{
      :name=:create-server
      :do=[|%fn|
        ^$.js(:http).createServer(%fn)
      ]
    }
    ~method{
      :name=:start
      :do=[|%port|
        .create-server([|%req %res|
          %path-handler=.path-handler(%req.url)
          %path-handler.if-null(
            [%res.end("404 Not Found")]
            [%path-handler.handle(%req %res)]
          )
        ]).listen(%port)
      ]
    }
  )
}

~case{
  :name=:basic-http-path-handlers
  :do=[
    %server=~http-server.new
    %handler=~request-handler{
      :name=:test-handler
      :server=%server
      :path="/"
      :do=[|%req %res|
        %res.writeHead(200 {:Content-Type="text/html"})
        %res.end("<!DOCTYPE html><html><body><h1>Hello, World!</h1></body></html>")
      ]
    }
    .assert-eq(%handler.name %server.path-handler("/").name)
    %server.start(8080)
  ]
}
